% Workshop on Multilevel Modeling 2 (Cross Classified Models)
% Andy Grogan-Kaylor
% `s c(current_date)` `s c(current_time)`

---
geometry: margin=1 in
---

<style>body {max-width: 90%;} h1 {color: #00274C;} h2 {color: #2F65A7;} p {font-size: 110%;} pre.stata {font-size: 110%;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;} code {color: red;}</style>

# Navigation

> When this document is presented in slide show format, some slides may be long, and you may need to scroll down to see the full slide. In slide show format `b` makes text bigger, and `s` makes text smaller.


```s/
    clear all

    quietly: cd "/Users/agrogan/Desktop/GitHub/multilevel-workshop/"
```

# Introduction

A two level multilevel model imagines that *Level 1* units are nested in *Level 2* units. A three level multilevel model imagines that *Level 1* units are nested in *Level 2* units, which are in turn nested in *Level 3*. 

A cross-classified model imagines that the nesting is not hierarchical, but rather that there are two sets of clusters or nestings in which individuals may be nested. 

# Setup

```s
    use "../multilevel-thinking/simulate-and-analyze-multilevel-data/simulated_multilevel_longitudinal_data.dta", clear
```

# Cross Classified Model

We can treat these random effects as being *cross classified*. 

This might be useful if we had data where individuals lived in different countries at different times. 

However, because `id` is in fact nested inside `country`, in this case, estimating the random effects as cross classified will be more time consuming, but will give us equivalent results to a three level model.

## Standard (Less Computationally Efficient) Syntax

The below syntax will take a very long time to run with the full sample, and thus we have commented it out.

```s
    * mixed outcome t warmth physical_punishment || _all: R.country || _all: R.id
	
	* est store crossed1
```
	
The documentation notes that we can use a *much* more computationally efficient version of the above command, which is what we do in these notes. The user can verify that both versions of the command will produce equivalent results.

In fact, at the end of handout we verify the similarity of both sets of syntax using a random sample.

## Cross Classified With Computationally Efficient Syntax

```s
	mixed outcome t warmth physical_punishment || _all: R.country || id:
	
	est store crossed2 // store crossed effects result
```

# Three Level Model

```s	
    mixed outcome t warmth physical_punishment || country: || id:  // 3 level w/ random intercepts only
	
	est store threelevel // store random intercept model
```
	
# Nice Table of Results of Three Level and Cross Classified Model

```s
    est table threelevel crossed2, ///
    b(%9.3f) star stats(N ll chi2) ///
    varwidth(20) modelwidth(15)
```

# Verification of Syntax Equivalence for Cross Classified Model {#majorsection}

```s

    keep if family <= 5 // random sample of families
	
	quietly mixed outcome t warmth physical_punishment || _all: R.country || _all: R.id
	
    est store crossed1A // less efficient syntax
	
	quietly mixed outcome t warmth physical_punishment || _all: R.country || id:
	
	est store crossed2A // more efficient syntax
	
	est table crossed1A crossed2A, ///
	b(%9.3f) star stats(N ll chi2) ///
    varwidth(20) modelwidth(15)
	
```

# QUESTIONS??? {#majorsection}
